plugins {
	id "net.neoforged.gradle" version "[6.0.13,7)"
	id "org.parchmentmc.librarian.forgegradle" version "[1,)"
	id "org.spongepowered.mixin" version "0.7-SNAPSHOT"
	id "eclipse"
	id "idea"
	id "maven-publish"
}

repositories {
	jcenter()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://libraries.minecraft.net/"
	}
	maven {
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		url = "https://repo.spongepowered.org/maven"
	}
	maven {
		url = "https://dvs1.progwml6.com/files/maven/"
		content {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights.tconstruct"
		}
	}
	maven {
		url = "https://maven.blamejared.com/"
		content {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	maven {
		url = "https://maven.theillusivec4.top/"
		content {
			includeGroup "top.theillusivec4.curios"
		}
	}
	maven {
		url = "https://maven.hellfiredev.net/"
		content {
			includeGroup "hellfirepvp.observerlib"
			includeGroup "hellfirepvp.astralsorcery"
		}
	}
	maven {
		url = "https://maven.architectury.dev/"
		content {
			includeGroup "me.shedaniel"
		}
	}
	maven {
		url = "https://maven.saps.dev/releases/"
		content {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	maven {
		url = "https://maven.tterrag.com/"
		content {
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.simibubi.create"
		}
	}
	maven {
		url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
		content {
			includeGroup "software.bernie.geckolib"
		}
	}
	maven {
		url = "https://maven.covers1624.net/"
		content {
			includeGroup "com.teamcofh"
		}
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

def mcVersion = "1.16.5"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.jaopca"
version = "3.4.4.28"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "parchment", version: "2022.03.06-1.16.5"
	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
	generateRunFolders = true
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			mods {
				"${modId}" {
					source sourceSets.main
				}
			}
		}
		client {
			jvmArg "-Xmx7G"
		}
		server {
			jvmArg "-Xmx3G"
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:1.16.5-36.2.39"
	annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

	implementation fg.deobf("mezz.jei:jei-1.16.5:7.8.0.1009")
	implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.2.526")
	implementation fg.deobf("top.theillusivec4.curios:curios-forge:1.16.5-4.0.8.2")
	implementation fg.deobf("software.bernie.geckolib:geckolib-forge-1.16.5:3.0.106")
	implementation fg.deobf("vazkii.patchouli:Patchouli:1.16.4-53.3")
	implementation fg.deobf("slimeknights.mantle:Mantle:1.16.5-1.6.157")
	implementation fg.deobf("com.teamcofh:cofh_core:1.16.5-1.5.2.22")
	implementation fg.deobf("dev.latvian.mods:rhino-forge:1605.1.5-build.75")
	implementation fg.deobf("me.shedaniel:architectury-forge:1.32.68")
	implementation fg.deobf("hellfirepvp.observerlib:observerlib:1.16.5-1.5.3.v78") {
		transitive = false
	}
	implementation fg.deobf("com.jozufozu.flywheel:Flywheel:1.16-0.2.5.53")
	implementation fg.deobf("dev.latvian.mods:kubejs-forge:1605.3.19-build.299") {
		transitive = false
	}
	implementation fg.deobf("net.darkhax.openloader:OpenLoader-1.16.5:9.0.3")
	implementation fg.deobf("appeng:appliedenergistics2:8.4.7")
	implementation fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:1.16.5-5.1.0-148")
	implementation fg.deobf("com.simibubi.create:Create:mc1.16.5_v0.3.2g+480")
	implementation fg.deobf("slimeknights.tconstruct:TConstruct:1.16.5-3.3.4.335")
	implementation fg.deobf("mekanism:Mekanism:1.16.5-10.1.2.457")
	implementation fg.deobf("mekanism:Mekanism:1.16.5-10.1.2.457:generators")
	implementation fg.deobf("dev.ftb.mods:ftb-industrial-contraptions:1605.1.3-build.122") {
		transitive = false
	}
	implementation fg.deobf("hellfirepvp.astralsorcery:astralsorcery-1.16.5:1.16.4-1.14.1.v106") {
		transitive = false
	}
	implementation fg.deobf("com.teamcofh:thermal_foundation:1.16.5-1.5.2.30")
	implementation fg.deobf("com.teamcofh:thermal_expansion:1.16.5-1.5.2.16")
	implementation fg.deobf("redsnakegames.mcenderdragon:futurepack:1.16.5-31.3.7006")

	//CurseMaven
	implementation fg.deobf("curse.maven:ato-405593:3405905")
	implementation fg.deobf("curse.maven:allthemodium-364466:3529763")
	implementation fg.deobf("curse.maven:silent-lib-242998:3385768")
	implementation fg.deobf("curse.maven:silents-mechanisms-326258:3272909")
	implementation fg.deobf("curse.maven:silents-gems-220311:3353519")
	implementation fg.deobf("curse.maven:silent-gear-297039:3585058")
	implementation fg.deobf("curse.maven:industrial-reborn-358877:3076845")
	implementation fg.deobf("curse.maven:essentials-293752:3393808")
	implementation fg.deobf("curse.maven:crossroads-mc-250231:3347113")
	implementation fg.deobf("curse.maven:fl-262385:3906730")
	implementation fg.deobf("curse.maven:flux-machinery-248942:3906729")
	implementation fg.deobf("curse.maven:blood-magic-224791:3870899")
	implementation fg.deobf("curse.maven:occultism-361026:3861416")
	implementation fg.deobf("curse.maven:themcbros-useless-mod-322455:3364720")
	implementation fg.deobf("curse.maven:electrodynamics-316867:3605071")
	implementation fg.deobf("curse.maven:cyclops-core-232758:3517568")
	implementation fg.deobf("curse.maven:common-capabilities-247007:3482420")
	implementation fg.deobf("curse.maven:integrated-dynamics-236307:3640128")
	implementation fg.deobf("curse.maven:voluminous-energy-416704:3508860")
	implementation fg.deobf("curse.maven:assemblylinemachines-388282:3493402")
	implementation fg.deobf("curse.maven:wtbw_lib-357089:3087379")
	implementation fg.deobf("curse.maven:wtbw_core-357091:3087383")
	implementation fg.deobf("curse.maven:wtbw_machines-357095:3087391")
	implementation fg.deobf("curse.maven:json-things-541617:3655282")

	//Performance
	implementation fg.deobf("curse.maven:shutup-experimental-settings-407174:3188120")
	implementation fg.deobf("curse.maven:modernfix-790626:4980927")
	implementation fg.deobf("curse.maven:ferritecore-429235:4074330")
	implementation fg.deobf("curse.maven:lazy-dfu-forge-460819:3249059")
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
	finalizedBy("reobfJar")
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

mixin {
	add sourceSets.main, "jaopca.refmap.json"
}
