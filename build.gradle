plugins {
	id "com.crystaelix.loom" version "jitpack-f4853e7a"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dvs1.progwml6.com/files/maven/"
			}
		}
		filter {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights.tconstruct"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.theillusivec4.top/"
			}
		}
		filter {
			includeGroup "top.theillusivec4.curios"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.architectury.dev/"
			}
		}
		filter {
			includeGroup "dev.architectury"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.saps.dev/releases/"
			}
		}
		filter {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.tterrag.com/"
			}
		}
		filter {
			includeGroup "com.tterrag.registrate"
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.simibubi.create"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
			}
		}
		filter {
			includeGroup "software.bernie.geckolib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "com.teamcofh"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1/"
			}
		}
		filter {
			includeGroup "com.kotori316"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://jenkins.bdew.net/maven/"
			}
		}
		filter {
			includeGroup "net.bdew"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dogforce-games.com/maven/"
			}
		}
		filter {
			includeGroup "dev.gigaherz.jsonthings"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
			}
		}
		filter {
			includeGroup "net.tslat.smartbrainlib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://nexus.themcbrothers.net/repository/maven-releases/"
			}
		}
		filter {
			includeGroup "net.themcbrothers"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "mekanism"
			includeGroup "redsnakegames.mcenderdragon"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.18.2"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "4"
def vendor = "thelm"

group = "thelm.jaopca"
version = "4.1.18.40"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	forge {
		mixinConfig "${modId}.mixins.json"
	}
	mixin.defaultRefmapName = "${modId}.refmap.json"
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client {
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.18.2"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.18.2:2022.11.06@zip"
	}
	forge "net.minecraftforge:forge:1.18.2-40.2.17"

	// Common
	modImplementation("mezz.jei:jei-1.18.2-forge:10.2.1.1006") {transitive = false}
	modImplementation "com.tterrag.registrate:Registrate:MC1.18.2-1.1.3"
	modImplementation "top.theillusivec4.curios:curios-forge:1.18.2-5.0.9.2"
	modImplementation "software.bernie.geckolib:geckolib-forge-1.18:3.0.57"
	modImplementation "vazkii.patchouli:Patchouli:1.18.2-71.1"
	modImplementation "dev.architectury:architectury-forge:4.11.93"

	// CraftTweaker
	modImplementation "com.blamejared.crafttweaker:CraftTweaker-forge-1.18.2:9.1.211"

	// KubeJS
	modImplementation "dev.latvian.mods:rhino-forge:1802.2.1-build.255"
	modImplementation("dev.latvian.mods:kubejs-forge:1802.5.5-build.569") {transitive = false}

	// Json Things
	modImplementation "dev.gigaherz.jsonthings:JsonThings-1.18.2:0.4.15"

	// Open Loader
	modImplementation "net.darkhax.openloader:OpenLoader-Forge-1.18.2:12.0.1"

	// Assembly Line Machines
	modImplementation "curse.maven:assemblylinemachines-388282:3949460"

	// Blood Magic
	modImplementation "curse.maven:blood-magic-224791:3953207"

	// Create
	modImplementation "com.jozufozu.flywheel:flywheel-forge-1.18.2:0.6.10-105"
	modImplementation("com.simibubi.create:create-1.18.2:0.5.1.f-332:slim") {transitive = false}

	// Crossroads
	modImplementation "curse.maven:essentials-293752:3796423"
	modImplementation "curse.maven:crossroads-mc-250231:4811933"

	// Cyclic
	modImplementation "curse.maven:cyclic-239286:4658622"

	// Electrodynamics
	modImplementation "curse.maven:electrodynamics-316867:5012980"

	// Factorium
	implementation("com.kotori316:scalablecatsforce:2.13.8-build-4:with-library") {transitive = false}
	modImplementation "net.bdew:bdlib:1.19.3.7-mc1.18.2"
	modImplementation "net.bdew:factorium:1.1.0.10-mc1.18.2"

	// FTB Industrial Contraptions
	modImplementation("dev.ftb.mods:ftb-library-forge:1802.3.11-build.177") {transitive = false}
	modImplementation("dev.ftb.mods:ftb-industrial-contraptions:1802.1.6-build.220") {transitive = false}

	// Futurepack
	modImplementation "redsnakegames.mcenderdragon:futurepack:1.18.2-33.0.7544"

	// Immersive Engineering
	modImplementation "blusunrize.immersiveengineering:ImmersiveEngineering:1.18.2-8.4.0-161"

	// Industrial Reborn
	modImplementation "curse.maven:industrial-reborn-358877:4034099"

	// Integrated Dynamics
	modImplementation "curse.maven:cyclops-core-232758:4905958"
	modImplementation "curse.maven:common-capabilities-247007:4987177"
	modImplementation "curse.maven:integrated-dynamics-236307:4987211"

	// LavaTech
	modImplementation "curse.maven:lavatech-533955:4154002"

	// Mekanism
	modImplementation "mekanism:Mekanism:1.18.2-10.2.5.465"

	// NuclearCraft: Neoteric
	modImplementation "curse.maven:nuclearcraft-neoteric-840010:5127366"

	// Occultism
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-forge-1.18.2:1.9"
	modImplementation "curse.maven:modonomicon-538392:4641770"
	modImplementation "curse.maven:occultism-361026:4729072"

	// Tinkers' Construct
	modImplementation "slimeknights.mantle:Mantle:1.18.2-1.9.45"
	modImplementation "slimeknights.tconstruct:TConstruct:1.18.2-3.6.4.113"

	// Thermal Expansion
	modImplementation "com.teamcofh:cofh_core:1.18.2-9.2.3.47"
	modImplementation "com.teamcofh:thermal_core:1.18.2-9.2.1.48"
	modImplementation "com.teamcofh:thermal_foundation:1.18.2-9.2.2.58"
	modImplementation "com.teamcofh:thermal_expansion:1.18.2-9.2.2.24"
	modImplementation "com.teamcofh:thermal_integration:1.18.2-9.2.1.18"

	// Useful Machinery
	modImplementation "net.themcbrothers:TheMCBrosLib:1.18.2-2.0.5"
	modImplementation "net.themcbrothers:useful-foundation:1.18.2-9.0.0"
	modImplementation "net.themcbrothers:useful-machinery:1.18.2-5.7.0"

	// Voluminous Energy
	modImplementation "curse.maven:voluminous-energy-416704:4627233"

	// Testing
	modImplementation "curse.maven:ato-405593:3975599"
	modImplementation "curse.maven:allthemodium-364466:4019806"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:4377180"
	implementation "io.github.llamalad7:mixinextras-forge:0.3.5"
	modImplementation "curse.maven:modernfix-790626:5077313"
	modImplementation "curse.maven:ferritecore-429235:4074294"
	modImplementation "curse.maven:lazy-dfu-forge-460819:3544496"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

def curseforgeDependencies = [
	"assemblylinemachines",
	"blood-magic",
	"crafttweaker",
	"create",
	"crossroads-mc",
	"cyclic",
	"electrodynamics",
	"factorium",
	"ftb-industrial-contraptions-forge",
	"futurepack",
	"immersive-engineering",
	"industrial-reborn",
	"integrated-dynamics",
	"json-things",
	"kubejs",
	"lavatech",
	"mekanism",
	"nuclearcraft-neoteric",
	"occultism",
	"open-loader",
	"tinkers-construct",
	"thermal-expansion",
	"useful-machinery",
	"voluminous-energy"
]

def modrinthDependencies = [
	"blood-magic",
	"crafttweaker",
	"create",
	"crossroads",
	"electrodynamics",
	"factorium",
	"futurepack",
	"immersiveengineering",
	"industrial-reborn",
	"integrated-dynamics",
	"kubejs",
	"mekanism",
	"occultism",
	"open-loader",
	"tinkers-construct",
	"thermal-expansion",
	"useful-machinery",
	"voluminous_energy"
]

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = STABLE
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "266936"
		minecraftVersions.add(mcVersion)
		optional curseforgeDependencies as String[]
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "P48mHi2j"
		minecraftVersions.add(mcVersion)
		optional modrinthDependencies as String[]
	}
}
