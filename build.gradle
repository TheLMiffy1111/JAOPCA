plugins {
	id "net.neoforged.gradle" version "[6.0.13,7)"
	id "org.parchmentmc.librarian.forgegradle" version "[1,)"
	id "org.spongepowered.mixin" version "0.7-SNAPSHOT"
	id "eclipse"
	id "idea"
	id "maven-publish"
}

repositories {
	jcenter()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		url = "https://repo.spongepowered.org/maven"
	}
	maven {
		url = "https://dvs1.progwml6.com/files/maven/"
		content {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights.tconstruct"
		}
	}
	maven {
		url = "https://maven.blamejared.com/"
		content {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	maven {
		url = "https://maven.theillusivec4.top/"
		content {
			includeGroup "top.theillusivec4.curios"
		}
	}
	maven {
		url = "https://maven.architectury.dev/"
		content {
			includeGroup "dev.architectury"
		}
	}
	maven {
		url = "https://maven.saps.dev/releases/"
		content {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	maven {
		url = "https://maven.tterrag.com/"
		content {
			includeGroup "com.simibubi.create"
		}
	}
	maven {
		url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
		content {
			includeGroup "software.bernie.geckolib"
		}
	}
	maven {
		url = "https://maven.covers1624.net/"
		content {
			includeGroup "com.teamcofh"
		}
	}
	maven {
		url = "https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1/"
		content {
			includeGroup "com.kotori316"
		}
	}
	maven {
		url = "https://jenkins.bdew.net/maven/"
		content {
			includeGroup "net.bdew"
		}
	}
	maven {
		url = "https://dogforce-games.com/maven/"
		content {
			includeGroup "dev.gigaherz.jsonthings"
		}
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

def mcVersion = "1.19.3"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "4"
def vendor = "thelm"

group = "thelm.jaopca"
version = "4.3.4.12"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "parchment", version: "2023.06.25-1.19.3"
	generateRunFolders = true
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			mods {
				"${modId}" {
					source sourceSets.main
				}
			}
		}
		client {
			jvmArg "-Xmx7G"
		}
		server {
			jvmArg "-Xmx3G"
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:1.19.3-44.1.23"
	annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

	implementation fg.deobf("mezz.jei:jei-1.19.3-forge:12.4.0.22")
	implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-1.19.3:11.0.2") {
		transitive = false
	}
	implementation fg.deobf("top.theillusivec4.curios:curios-forge:1.19.3-5.1.4.1")
	implementation fg.deobf("software.bernie.geckolib:geckolib-forge-1.19.3:4.0.6")
	implementation fg.deobf("vazkii.patchouli:Patchouli:1.19.3-78")
	implementation fg.deobf("dev.architectury:architectury-forge:7.1.86")
	implementation("com.kotori316:scalablecatsforce:2.13.10-build-15:with-library") {
		transitive = false
	}
	implementation fg.deobf("net.darkhax.openloader:OpenLoader-Forge-1.19.3:16.0.1")
	implementation fg.deobf("dev.gigaherz.jsonthings:JsonThings-1.19.3:0.7.12")
	implementation fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:1.19.3-9.3.0-163")

	//CurseMaven
	implementation fg.deobf("curse.maven:voluminous-energy-416704:4627266")
	implementation fg.deobf("curse.maven:cyclops-core-232758:4438259")
	implementation fg.deobf("curse.maven:common-capabilities-247007:4391468")
	implementation fg.deobf("curse.maven:integrated-dynamics-236307:4445211")
	implementation fg.deobf("curse.maven:assemblylinemachines-388282:4529291")
	implementation fg.deobf("curse.maven:smartbrainlib-661293:4458574")
	implementation fg.deobf("curse.maven:modonomicon-538392:4641897")
	implementation fg.deobf("curse.maven:occultism-361026:4641941")
	implementation fg.deobf("curse.maven:cyclic-239286:4578692")
	implementation fg.deobf("curse.maven:bdlib-70496:4356501")
	implementation fg.deobf("curse.maven:factorium-583423:4362035")
	implementation fg.deobf("curse.maven:energized-power-782147:4924275")

	//Performance
	implementation fg.deobf("curse.maven:yeetusexperimentus-635427:3909789")
	implementation fg.deobf("curse.maven:ferritecore-429235:4147336")
	implementation fg.deobf("curse.maven:lazy-dfu-forge-460819:4327266")
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
	finalizedBy("reobfJar")
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

mixin {
	add sourceSets.main, "jaopca.refmap.json"
}
