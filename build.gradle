plugins {
	id "com.crystaelix.loom" version "jitpack-6453f0f4"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dvs1.progwml6.com/files/maven/"
			}
		}
		filter {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights.tconstruct"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.theillusivec4.top/"
			}
		}
		filter {
			includeGroup "top.theillusivec4.curios"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.architectury.dev/"
			}
		}
		filter {
			includeGroup "dev.architectury"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.saps.dev/releases/"
			}
		}
		filter {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.tterrag.com/"
			}
		}
		filter {
			includeGroup "com.tterrag.registrate"
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.simibubi.create"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
			}
		}
		filter {
			includeGroup "com.eliotlash.mclib"
			includeGroup "software.bernie.geckolib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "com.teamcofh"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1/"
			}
		}
		filter {
			includeGroup "com.kotori316"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://jenkins.bdew.net/maven/"
			}
		}
		filter {
			includeGroup "net.bdew"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dogforce-games.com/maven/"
			}
		}
		filter {
			includeGroup "dev.gigaherz.jsonthings"
			includeGroup "dev.gigaherz.graph"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
			}
		}
		filter {
			includeGroup "net.tslat.smartbrainlib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
			}
		}
		filter {
			includeGroup "com.klikli_dev"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://nexus.themcbrothers.net/repository/maven-releases/"
			}
		}
		filter {
			includeGroup "net.themcbrothers"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.20.1"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "4"
def vendor = "thelm"

group = "thelm.jaopca"
version = "4.4.10.17"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	forge {
		mixinConfig "${modId}.mixins.json"
		useForgeLoggerConfig = true
	}
	mixin.defaultRefmapName = "${modId}.refmap.json"
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "forge.logging.console.level", "debug"
		}
		client {
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.20.1"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip"
	}
	forge "net.minecraftforge:forge:1.20.1-47.3.1"

	// Common
	modImplementation("mezz.jei:jei-1.20.1-forge:15.3.0.8") {transitive = false}
	modImplementation "com.tterrag.registrate:Registrate:MC1.20-1.3.11"
	modImplementation "top.theillusivec4.curios:curios-forge:5.9.1+1.20.1"
	forgeRuntimeLibrary "com.eliotlash.mclib:mclib:20"
	modImplementation("software.bernie.geckolib:geckolib-forge-1.20.1:4.4.7") {transitive = false}
	modImplementation "vazkii.patchouli:Patchouli:1.20.1-84-FORGE"
	forgeRuntimeLibrary "org.commonmark:commonmark:0.22.0"
	forgeRuntimeLibrary "org.commonmark:commonmark-ext-gfm-strikethrough:0.22.0"
	forgeRuntimeLibrary "org.commonmark:commonmark-ext-ins:0.22.0"
	modImplementation("com.klikli_dev:modonomicon-1.20.1-forge:1.73.0") {transitive = false}
	modImplementation "dev.architectury:architectury-forge:9.2.14"

	// CraftTweaker
	modImplementation("com.blamejared.crafttweaker:CraftTweaker-forge-1.20.1:14.0.40") {transitive = false}

	// KubeJS
	implementation "dev.latvian.mods:rhino-forge:2001.2.2-build.18"
	modImplementation("dev.latvian.mods:kubejs-forge:2001.6.5-build.14") {transitive = false}

	// Json Things
	modImplementation "dev.gigaherz.jsonthings:JsonThings-1.20.1:0.9.1"

	// Open Loader
	modImplementation "net.darkhax.openloader:OpenLoader-Forge-1.20.1:19.0.4"

	// Blood Magic
	modImplementation "curse.maven:blood-magic-224791:5290993"

	// Create
	modImplementation "com.jozufozu.flywheel:flywheel-forge-1.20.1:0.6.10-8"
	modImplementation("com.simibubi.create:create-1.20.1:0.5.1.f-26:slim") {transitive = false}

	// Crossroads
	modImplementation "curse.maven:essentials-293752:4640300"
	modImplementation "curse.maven:crossroads-mc-250231:5208926"

	// Cyclic
	modImplementation "curse.maven:flib-661261:5262803"
	modImplementation "curse.maven:cyclic-239286:5259070"

	// Electrodynamics
	modImplementation "curse.maven:electrodynamics-316867:5446489"

	// Embers Rekindled
	modImplementation "curse.maven:embers-rekindled-300777:5441842"

	// Ender IO
	implementation "dev.gigaherz.graph:GraphLib3:3.0.5"
	modImplementation "curse.maven:ender-io-64578:5460093"

	// Energized Power
	modImplementation "curse.maven:energized-power-782147:5421222"

	// Factorium
	implementation("com.kotori316:scalablecatsforce:3.3.0-build-2:with-library") {transitive = false}
	modImplementation "curse.maven:bdlib-70496:4670650"
	modImplementation "curse.maven:factorium-583423:4670915"

	// Immersive Engineering
	modImplementation "blusunrize.immersiveengineering:ImmersiveEngineering:1.20.1-10.0.0-169.40"

	// Industrial Reborn

	// Integrated Dynamics
	modImplementation "curse.maven:cyclops-core-232758:5262063"
	modImplementation "curse.maven:common-capabilities-247007:4987207"
	modImplementation "curse.maven:integrated-dynamics-236307:5297722"

	// Mekanism
	modImplementation "mekanism:Mekanism:1.20.1-10.4.8.43"

	// NuclearCraft: Neoteric
	modImplementation "curse.maven:nuclearcraft-neoteric-840010:5209150"

	// Occultism
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-forge-1.20.1:1.13"
	modImplementation("com.klikli_dev:occultism-1.20.1:1.132.1") {transitive = false}

	// Railcraft Reborn
	modImplementation "curse.maven:railcraft-reborn-901491:5242853"

	// Thermal Expansion
	modImplementation "com.teamcofh:cofh_core:1.20.1-11.0.2.56"
	modImplementation "com.teamcofh:thermal_core:1.20.1-11.0.4.22"
	modImplementation("com.teamcofh:thermal_foundation:1.20.1-11.0.4.68") {transitive = false}
	modImplementation "com.teamcofh:thermal_expansion:1.20.1-11.0.1.29"
	modImplementation "com.teamcofh:thermal_integration:1.20.1-11.0.1.27"

	// Theurgy
	modImplementation("com.klikli_dev:theurgy-1.20.1:1.19.2") {transitive = false}

	// Useful Machinery
	modImplementation "net.themcbrothers:TheMCBrosLib:1.20.1-4.2.0"
	modImplementation "net.themcbrothers:useful-foundation:1.20.1-2.0.1"
	modImplementation "net.themcbrothers:useful-machinery:1.20.1-3.0.1"

	// Voluminous Energy
	modImplementation "curse.maven:voluminous-energy-416704:5170889"

	// Testing
	modImplementation "curse.maven:ato-405593:5348605"
	modImplementation "curse.maven:allthemodium-364466:5439699"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:4680832"
	implementation "io.github.llamalad7:mixinextras-forge:0.3.6"
	modImplementation "curse.maven:modernfix-790626:5425647"
	modImplementation "curse.maven:ferritecore-429235:4810975"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

def curseforgeDependencies = [
	"blood-magic",
	"crafttweaker",
	"create",
	"crossroads-mc",
	"cyclic",
	"electrodynamics",
	"embers-rekindled",
	"energized-power",
	"factorium",
	"immersive-engineering",
	"industrial-reborn",
	"integrated-dynamics",
	"json-things",
	"kubejs",
	"mekanism",
	"nuclearcraft-neoteric",
	"occultism",
	"open-loader",
	"railcraft-reborn",
	"thermal-expansion",
	"theurgy",
	"useful-machinery",
	"voluminous-energy"
]

def modrinthDependencies = [
	"blood-magic",
	"crafttweaker",
	"create",
	"crossroads",
	"electrodynamics",
	"embers",
	"energized-power",
	"factorium",
	"immersiveengineering",
	"industrial-reborn",
	"integrated-dynamics",
	"kubejs",
	"mekanism",
	"occultism",
	"open-loader",
	"railcraft-reborn",
	"thermal-expansion",
	"theurgy",
	"useful-machinery",
	"voluminous_energy"
]

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = STABLE
	modLoaders.addAll("forge", "neoforge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "266936"
		minecraftVersions.add(mcVersion)
		optional curseforgeDependencies as String[]
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "P48mHi2j"
		minecraftVersions.add(mcVersion)
		optional modrinthDependencies as String[]
	}
}
