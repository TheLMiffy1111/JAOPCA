plugins {
	id "com.crystaelix.loom" version "jitpack-6cccf24f"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "com.github.johnrengelman.shadow" version "[8,9)"
	id "me.modmuss50.mod-publish-plugin" version "0.7.2"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "CraftTweaker2"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dvs1.progwml6.com/files/maven/"
			}
		}
		filter {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.amadornes.com/"
			}
		}
		filter {
			includeGroup "MCMultiPart2"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.tterrag.com/"
			}
		}
		filter {
			includeGroup "com.enderio.core"
			includeGroup "com.enderio"
			includeGroup "info.loenwind.autoconfig"
			includeGroup "info.loenwind.autosave"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.modmuss50.me/"
			}
		}
		filter {
			includeGroup "RebornCore"
			includeGroup "TechReborn"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "codechicken"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.shadowfacts.net/"
			}
		}
		filter {
			includeGroup "net.shadowfacts"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.mcmoddev.com/"
			}
		}
		filter {
			includeGroup "net.ndrei"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://www.mod-buildcraft.com/maven/"
			}
		}
		filter {
			includeGroup "com.mod-buildcraft"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://repo.cleanroommc.com/releases/"
				metadataSources {
					mavenPom()
					ignoreGradleMetadataRedirection()
				}
			}
		}
		filter {
			includeGroup "com.cleanroommc"
			includeGroup "zone.rong"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "net.industrial-craft"
			includeGroup "net.sengir.forestry"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.12.2"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "2.3"
def vendor = "thelm"

group = "thelm.jaopca"
version = "2.3.11.27"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
			environmentVariable "MC_VERSION", mcVersion
		}
		client {
			programArg "--username=Dev"
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
	generatedIntermediateMappings()
}

configurations {
	shadowImplementation
	implementation.extendsFrom(shadowImplementation)
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.12.2"
	mappings "de.oceanlabs.mcp:mcp_stable:39-1.12"
	legacyForge "net.minecraftforge:forge:1.12.2-14.23.5.2860"

	// Shadow
	shadowImplementation "com.electronwill.night-config:core:3.6.4"
	shadowImplementation "com.electronwill.night-config:toml:3.6.4"

	// Common
	implementation "curse.maven:wrapup-275153:2486198"
	modImplementation "mezz.jei:jei_1.12.2:4.16.1.1012"
	modImplementation "codechicken:CodeChickenLib:1.12.2-3.2.3.358:universal"
	implementation "net.shadowfacts:Forgelin:1.8.4"
	modImplementation "curse.maven:baubles-227083:2518667"
	modImplementation "curse.maven:redstone-flux-270789:2920436"
	implementation("zone.rong:mixinbooter:9.3") {transitive = false}

	// CraftTweaker
	modImplementation("CraftTweaker2:ZenScript:4.1.20.700") {transitive = false}
	modImplementation("CraftTweaker2:CraftTweaker2-API:4.1.20.700") {transitive = false}
	modImplementation("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.700") {transitive = false}

	// GroovyScript
	modImplementation("com.cleanroommc:groovyscript:1.1.3") {transitive = false}

	// AbyssalCraft
	modImplementation "curse.maven:abyssalcraft-53686:5330323"

	// Advanced Rocketry
	modImplementation "curse.maven:libvulpes-236541:3801015"
	modImplementation "curse.maven:advanced-rocketry-236542:4671856"

	// Applied Energistics 2
	modImplementation "curse.maven:applied-energistics-2-223794:2747063"

	// Astral Sorcery
	modImplementation "curse.maven:astral-sorcery-241721:3044416"

	// BC Ore Processing
	modImplementation "net.sengir.forestry:forestry_1.12.2:5.8.2.422"
	modImplementation "com.mod-buildcraft:buildcraft-main:7.99.24.8"
	modImplementation "curse.maven:buildcraft-compat-233140:3204471"
	modImplementation "curse.maven:bc-ore-processing-277910:2882663"

	// Embers Rekindled
	modImplementation "curse.maven:embers-rekindled-300777:3695248"

	// Ender IO
	modImplementation "com.enderio.core:EnderCore:1.12.2-0.5.78"
	modImplementation "com.enderio.core:EnderCore:1.12.2-0.5.78:core"
	modImplementation "info.loenwind.autoconfig:AutoConfig:1.12.2-1.0.2"
	modImplementation "info.loenwind.autosave:AutoSave:1.12.2-1.0.11"
	modImplementation("com.enderio:EnderIO:1.12.2-5.3.72") {transitive = false}

	// EssentialCraft
	modImplementation "curse.maven:dummycore-unofficial-266491:2611426"
	modImplementation "curse.maven:essentialcraft-4-unofficial-254817:5416404"

	// Foundry
	modImplementation "curse.maven:cofh-core-69162:2920433"
	modImplementation "curse.maven:cofh-world-271384:2920434"
	modImplementation "curse.maven:thermal-foundation-222880:2926428"
	modImplementation "curse.maven:placebo-283644:4629597"
	modImplementation "curse.maven:ignition-foundry-283807:2719054"

	// Futurepack
	modImplementation "curse.maven:futurepack-237333:2856874"

	// HBM's Nuclear Tech Mod
	modImplementation "curse.maven:hbms-nuclear-tech-mod-extended-edition-708939:5254559"

	// IndustrialCraft 2
	modImplementation "net.industrial-craft:industrialcraft-2:2.8.222-ex112"

	// Integrated Dynamics
	modImplementation "curse.maven:cyclops-core-232758:3159497"
	modImplementation "curse.maven:common-capabilities-247007:3053017"
	modImplementation "curse.maven:integrated-dynamics-236307:3159505"

	// Magneticraft
	modImplementation "curse.maven:modelloader-277663:2744735"
	modImplementation "curse.maven:magneticraft-224808:3791484"

	// Mekanism
	modImplementation "MCMultiPart2:MCMultiPart:2.5.3"
	modImplementation "curse.maven:mekanism-268560:2835175"

	// Minestrappolation
	modImplementation "curse.maven:minestrappolation-5-233351:2860018"

	// Powered Thingies
	modImplementation "curse.maven:tesla-core-lib-254602:3438487"
	modImplementation "curse.maven:powered-thingies-264283:2556740"

	// QMD
	modImplementation "curse.maven:nuclearcraft-overhauled-336895:5446445"
	modImplementation "curse.maven:nco-java-legacy-lib-1032769:5414463"
	modImplementation "curse.maven:qmd-362056:5681949"

	// Thaumcraft
	modImplementation "curse.maven:thaumcraft-223628:2629023"
	modImplementation "curse.maven:thaumic-jei-285492:2705304"

	// Railcraft
	modImplementation "curse.maven:railcraft-51195:3853491"

	// Tech Reborn
	modImplementation("RebornCore:RebornCore-1.12.2:3.19.4.529:universal") {transitive = false}
	modImplementation("TechReborn:TechReborn-ModCompatibility-1.12.2:1.4.0.76:universal") {transitive = false}
	modImplementation("TechReborn:TechReborn-1.12.2:2.27.3.1084:universal") {transitive = false}

	// Tinkers' Construct
	modImplementation "slimeknights.mantle:Mantle:1.12-1.3.3.55"
	modImplementation "slimeknights:TConstruct:1.12.2-2.13.0.183"

	// Performance
	modImplementation "curse.maven:reid-629017:5502915"
	modImplementation "curse.maven:vintagefix-871198:5536276"
	modImplementation "curse.maven:lolasm-460609:5257348"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml", "META-INF/neoforge.mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	archiveClassifier = "slim-dev"
	manifest.attributes(manifestAttributes)
}

shadowJar {
	archiveClassifier = "dev"
	destinationDirectory = layout.buildDirectory.dir("devlibs")
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	configurations = [project.configurations.shadowImplementation]
	enableRelocation true
	relocationPrefix "thelm"
}

remapJar {
	dependsOn shadowJar
	inputFile = shadowJar.archiveFile
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

def curseforgeDependencies = [
	"abyssalcraft",
	"advanced-rocketry",
	"applied-energistics-2",
	"astral-sorcery",
	"bc-ore-processing",
	"crafttweaker",
	"embers-rekindled",
	"ender-io",
	"essentialcraft-4-unofficial",
	"futurepack",
	"groovyscript",
	"hbms-nuclear-tech-mod-reloaded",
	"ignition-foundry",
	"industrial-craft",
	"integrated-dynamics",
	"magneticraft",
	"mekanism",
	"minestrappolation-5",
	"powered-thingies",
	"qmd",
	"railcraft",
	"techreborn",
	"tinkers-construct",
	"thaumcraft"
]

def modrinthDependencies = [
	"ae2",
	"crafttweaker",
	"embers",
	"enderio",
	"futurepack",
	"groovyscript",
	"hbm-reloaded",
	"industrial-craft",
	"integrated-dynamics",
	"mekanism",
	"tinkers-construct",
]

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = STABLE
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "266936"
		minecraftVersions.add(mcVersion)
		requires "wrapup"
		optional curseforgeDependencies as String[]
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "P48mHi2j"
		minecraftVersions.add(mcVersion)
		requires "wrapup"
		optional modrinthDependencies as String[]
	}
}
