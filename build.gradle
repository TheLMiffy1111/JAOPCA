plugins {
	id "com.crystaelix.loom" version "jitpack-06d03f31"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dvs1.progwml6.com/files/maven/"
			}
		}
		filter {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights.tconstruct"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.theillusivec4.top/"
			}
		}
		filter {
			includeGroup "top.theillusivec4.curios"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.hellfiredev.net/"
			}
		}
		filter {
			includeGroup "hellfirepvp.observerlib"
			includeGroup "hellfirepvp.astralsorcery"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.architectury.dev/"
			}
		}
		filter {
			includeGroup "me.shedaniel"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.saps.dev/releases/"
			}
		}
		filter {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.tterrag.com/"
			}
		}
		filter {
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.simibubi.create"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
			}
		}
		filter {
			includeGroup "software.bernie.geckolib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "com.teamcofh"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "appeng"
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.16.5"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.jaopca"
version = "3.4.4.31"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	forge {
		mixinConfig "${modId}.mixins.json"
	}
	mixin.defaultRefmapName = "${modId}.refmap.json"
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client {
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.16.5"
	mappings loom.layered {
		mappings("de.oceanlabs.mcp:mcp_config:1.16.5-20210115.111550@zip") {
			mappingPath "config/joined.tsrg"
			fallbackNamespaces "official", "named"
			mergeNamespace "official"
		}
		officialMojangMappings {
			skipClassNames = true
		}
		parchment "org.parchmentmc.data:parchment-1.16.5:2022.03.06@zip"
	}
	forge "net.minecraftforge:forge:1.16.5-36.2.39"

	// Common
	modImplementation "mezz.jei:jei-1.16.5:7.8.0.1009"
	modImplementation "top.theillusivec4.curios:curios-forge:1.16.5-4.0.8.2"
	modImplementation "software.bernie.geckolib:geckolib-forge-1.16.5:3.0.106"
	modImplementation "vazkii.patchouli:Patchouli:1.16.4-53.3"
	modImplementation("me.shedaniel:architectury-forge:1.32.68") {transitive = false}

	// CraftTweaker
	modImplementation "com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.2.526"

	// KubeJS
	modImplementation "dev.latvian.mods:rhino-forge:1605.1.5-build.75"
	modImplementation("dev.latvian.mods:kubejs-forge:1605.3.19-build.299") {transitive = false}

	// Json Things
	modImplementation "curse.maven:json-things-541617:3655282"

	// Open Loader
	modImplementation "net.darkhax.openloader:OpenLoader-1.16.5:9.0.3"

	// Applied Energistics 2
	modImplementation "appeng:appliedenergistics2:8.4.7"

	// Assembly Line Machines
	modImplementation "curse.maven:assemblylinemachines-388282:3493402"

	// Astral Sorcery
	modImplementation("hellfirepvp.observerlib:observerlib:1.16.5-1.5.3.v78") {transitive = false}
	modImplementation("hellfirepvp.astralsorcery:astralsorcery-1.16.5:1.16.4-1.14.1.v106") {transitive = false}

	// Blood Magic
	modImplementation "curse.maven:blood-magic-224791:3870899"

	// Create
	modImplementation "com.jozufozu.flywheel:Flywheel:1.16-0.2.5.53"
	modImplementation "com.simibubi.create:Create:mc1.16.5_v0.3.2g+480"

	// Crossroads
	modImplementation "curse.maven:essentials-293752:3393808"
	modImplementation "curse.maven:crossroads-mc-250231:3347113"

	// Electrodynamics
	modImplementation "curse.maven:electrodynamics-316867:5002149"

	// Flux Machinery
	modImplementation "curse.maven:fl-262385:3906730"
	modImplementation "curse.maven:flux-machinery-248942:3906729"

	// FTB Industrial Contraptions
	modImplementation("dev.ftb.mods:ftb-industrial-contraptions:1605.1.3-build.122") {transitive = false}

	// Futurepack
	modImplementation "curse.maven:futurepack-237333:4019689"

	// Immersive Engineering
	modImplementation("blusunrize.immersiveengineering:ImmersiveEngineering:1.16.5-5.1.0-148") {transitive = false}

	// Industrial Reborn
	modImplementation "curse.maven:industrial-reborn-358877:3076845"

	// Integrated Dynamics
	modImplementation "curse.maven:cyclops-core-232758:3900678"
	modImplementation "curse.maven:common-capabilities-247007:3482420"
	modImplementation "curse.maven:integrated-dynamics-236307:3963794"

	// Mekanism
	modImplementation "mekanism:Mekanism:1.16.5-10.1.2.457"

	// Occultism
	modImplementation "curse.maven:occultism-361026:3861416"

	// Silent's Mechanisms
	modImplementation "curse.maven:silent-lib-242998:3385768"
	modImplementation "curse.maven:silents-mechanisms-326258:3272909"
	modImplementation "curse.maven:silents-gems-220311:3353519"
	modImplementation "curse.maven:silent-gear-297039:3585058"

	// Thermal Expansion
	modImplementation "com.teamcofh:cofh_core:1.16.5-1.5.2.22"
	modImplementation "com.teamcofh:thermal_foundation:1.16.5-1.5.2.30"
	modImplementation "com.teamcofh:thermal_expansion:1.16.5-1.5.2.16"

	// Tinkers' Construct
	modImplementation "slimeknights.mantle:Mantle:1.16.5-1.6.157"
	modImplementation "slimeknights.tconstruct:TConstruct:1.16.5-3.3.4.335"

	// Voluminous Energy
	modImplementation "curse.maven:voluminous-energy-416704:3508860"

	// WTBW Machines
	modImplementation "curse.maven:wtbw_lib-357089:3087379"
	modImplementation "curse.maven:wtbw_core-357091:3087383"
	modImplementation "curse.maven:wtbw_machines-357095:3087391"

	// Testing
	modImplementation "curse.maven:ato-405593:3405905"
	modImplementation "curse.maven:allthemodium-364466:3529763"

	// Performance
	modImplementation "curse.maven:shutup-experimental-settings-407174:3188120"
	modImplementation "curse.maven:modernfix-790626:5077294"
	modImplementation "curse.maven:ferritecore-429235:3435176"
	modImplementation "curse.maven:lazy-dfu-forge-460819:3249059"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

def curseforgeDependencies = [
	"applied-energistics-2",
	"assemblylinemachines",
	"astral-sorcery",
	"blood-magic",
	"crafttweaker",
	"create",
	"crossroads-mc",
	"electrodynamics",
	"flux-machinery",
	"ftb-industrial-contraptions-forge",
	"futurepack",
	"immersive-engineering",
	"industrial-reborn",
	"integrated-dynamics",
	"json-things",
	"kubejs",
	"mekanism",
	"occultism",
	"open-loader",
	"silents-mechanisms",
	"tinkers-construct",
	"thermal-expansion",
	"voluminous-energy",
	"wtbw_machines"
]

def modrinthDependencies = [
	"ae2",
	"blood-magic",
	"crafttweaker",
	"create",
	"crossroads",
	"electrodynamics",
	"futurepack",
	"immersiveengineering",
	"industrial-reborn",
	"integrated-dynamics",
	"kubejs",
	"mekanism",
	"occultism",
	"open-loader",
	"tinkers-construct",
	"thermal-expansion",
	"voluminous_energy"
]

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = BETA
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "266936"
		minecraftVersions.add(mcVersion)
		optional curseforgeDependencies as String[]
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "P48mHi2j"
		minecraftVersions.add(mcVersion)
		optional modrinthDependencies as String[]
	}
}
