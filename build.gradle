plugins {
	id "com.crystaelix.loom" version "jitpack-f4853e7a"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.theillusivec4.top/"
			}
		}
		filter {
			includeGroup "top.theillusivec4.curios"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.architectury.dev/"
			}
		}
		filter {
			includeGroup "dev.architectury"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.saps.dev/releases/"
			}
		}
		filter {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.tterrag.com/"
			}
		}
		filter {
			includeGroup "com.tterrag.registrate"
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.simibubi.create"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
			}
		}
		filter {
			includeGroup "software.bernie.geckolib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "com.teamcofh"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1/"
			}
		}
		filter {
			includeGroup "com.kotori316"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://jenkins.bdew.net/maven/"
			}
		}
		filter {
			includeGroup "net.bdew"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dogforce-games.com/maven/"
			}
		}
		filter {
			includeGroup "dev.gigaherz.jsonthings"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
			}
		}
		filter {
			includeGroup "net.tslat.smartbrainlib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
			}
		}
		filter {
			includeGroup "com.klikli_dev"
			includeGroup "com.github.klikli_dev"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://nexus.themcbrothers.net/repository/maven-releases/"
			}
		}
		filter {
			includeGroup "net.themcbrothers"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.19.2"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "4"
def vendor = "thelm"

group = "thelm.jaopca"
version = "4.2.10.27"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	forge {
		mixinConfig "${modId}.mixins.json"
	}
	mixin.defaultRefmapName = "${modId}.refmap.json"
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
			property "production", "true"
		}
		client {
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.19.2"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.19.2:2022.11.27@zip"
	}
	forge "net.minecraftforge:forge:1.19.2-43.3.8"

	// Common
	modImplementation("mezz.jei:jei-1.19.2-forge:11.6.0.1018") {transitive = false}
	modImplementation "com.tterrag.registrate:Registrate:MC1.19-1.1.5"
	modImplementation "top.theillusivec4.curios:curios-forge:1.19.2-5.1.6.1"
	modImplementation "software.bernie.geckolib:geckolib-forge-1.19:3.1.40"
	modImplementation "vazkii.patchouli:Patchouli:1.19.2-77"
	modImplementation "dev.architectury:architectury-forge:6.5.90"

	// CraftTweaker
	modImplementation "com.blamejared.crafttweaker:CraftTweaker-forge-1.19.2:10.1.53"

	// KubeJS
	modImplementation "dev.latvian.mods:rhino-forge:1902.2.3-build.284"
	modImplementation("dev.latvian.mods:kubejs-forge:1902.6.2-build.59") {transitive = false}

	// Json Things
	modImplementation "dev.gigaherz.jsonthings:JsonThings-1.19.2:0.7.15"

	// Open Loader
	modImplementation "net.darkhax.openloader:OpenLoader-Forge-1.19.2:15.0.2"

	// Assembly Line Machines
	modImplementation "curse.maven:assemblylinemachines-388282:3964948"

	// Create
	modImplementation "com.jozufozu.flywheel:flywheel-forge-1.19.2:0.6.10-20"
	modImplementation("com.simibubi.create:create-1.19.2:0.5.1.f-46:slim") {transitive = false}

	// Crossroads
	modImplementation "curse.maven:essentials-293752:4633275"
	modImplementation "curse.maven:crossroads-mc-250231:4633319"

	// Cyclic
	modImplementation "curse.maven:cyclic-239286:4660268"

	// Electrodynamics
	modImplementation "curse.maven:electrodynamics-316867:5040593"

	// Energized Power
	modImplementation "curse.maven:energized-power-782147:5153672"

	// Factorium
	implementation("com.kotori316:scalablecatsforce:2.13.8-build-8:with-library") {transitive = false}
	modImplementation "net.bdew:bdlib:1.25.0.5-mc1.19.2"
	modImplementation "net.bdew:factorium:1.4.0.4-mc1.19.2"

	// FTB Industrial Contraptions
	modImplementation("dev.ftb.mods:ftb-library-forge:1902.4.1-build.236") {transitive = false}
	modImplementation("dev.ftb.mods:ftb-industrial-contraptions:1900.1.7-build.221") {transitive = false}

	// IC2 Classic
	modImplementation "curse.maven:ic2-classic-242942:4879119"

	// Immersive Engineering
	modImplementation "blusunrize.immersiveengineering:ImmersiveEngineering:1.19.2-9.2.3-167"

	// Industrial Reborn
	modImplementation "curse.maven:industrial-reborn-358877:4651636"

	// Integrated Dynamics
	modImplementation "curse.maven:cyclops-core-232758:5080403"
	modImplementation "curse.maven:common-capabilities-247007:4987191"
	modImplementation "curse.maven:integrated-dynamics-236307:5087866"

	// Mekanism
	modImplementation "mekanism:Mekanism:1.19.2-10.3.9.13"

	// NuclearCraft: Neoteric
	modImplementation "curse.maven:nuclearcraft-neoteric-840010:4975776"

	// Occultism
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-forge-1.19.2:1.9"
	modImplementation "com.klikli_dev:modonomicon-1.19.2:1.34.0"
	modImplementation("com.github.klikli_dev:occultism-1.19.2:1.89.0") {transitive = false}

	// Silent's Mechanisms
	modImplementation "curse.maven:silent-lib-242998:3968715"
	modImplementation "curse.maven:silents-mechanisms-326258:4392434"
	modImplementation "curse.maven:silents-gems-220311:4181316"
	modImplementation "curse.maven:silent-gear-297039:4585760"

	// Thermal Expansion
	modImplementation "com.teamcofh:cofh_core:1.19.2-10.3.1.48"
	modImplementation "com.teamcofh:thermal_core:1.19.2-10.3.2.11"
	modImplementation("com.teamcofh:thermal_foundation:1.19.2-10.3.1.57:all") {transitive = false}
	modImplementation "com.teamcofh:thermal_expansion:1.19.2-10.3.1.25"
	modImplementation "com.teamcofh:thermal_integration:1.19.2-10.3.0.19"

	// Useful Machinery
	modImplementation "net.themcbrothers:TheMCBrosLib:1.19.2-2.0.5"
	modImplementation "net.themcbrothers:useful-foundation:1.19.2-3.0.0"
	modImplementation "net.themcbrothers:useful-machinery:1.19.2-4.1.0"

	// Voluminous Energy
	modImplementation "curse.maven:voluminous-energy-416704:4627252"

	// Testing
	modImplementation "curse.maven:ato-405593:4046926"
	modImplementation "curse.maven:allthemodium-364466:5040859"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:3909789"
	implementation "io.github.llamalad7:mixinextras-forge:0.3.5"
	modImplementation "curse.maven:modernfix-790626:5157931"
	modImplementation "curse.maven:ferritecore-429235:4117906"
	modImplementation "curse.maven:lazy-dfu-forge-460819:4327266"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

def curseforgeDependencies = [
	"assemblylinemachines",
	"crafttweaker",
	"create",
	"crossroads-mc",
	"cyclic",
	"electrodynamics",
	"energized-power",
	"factorium",
	"ftb-industrial-contraptions-forge",
	"ic2-classic",
	"immersive-engineering",
	"industrial-reborn",
	"integrated-dynamics",
	"json-things",
	"kubejs",
	"mekanism",
	"nuclearcraft-neoteric",
	"occultism",
	"open-loader",
	"thermal-expansion",
	"useful-machinery",
	"voluminous-energy"
]

def modrinthDependencies = [
	"crafttweaker",
	"create",
	"crossroads",
	"electrodynamics",
	"energized-power",
	"factorium",
	"immersiveengineering",
	"industrial-reborn",
	"integrated-dynamics",
	"kubejs",
	"mekanism",
	"occultism",
	"open-loader",
	"thermal-expansion",
	"useful-machinery",
	"voluminous_energy"
]

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = STABLE
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "266936"
		minecraftVersions.add(mcVersion)
		optional curseforgeDependencies as String[]
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "P48mHi2j"
		minecraftVersions.add(mcVersion)
		optional modrinthDependencies as String[]
	}
}
