plugins {
	id "net.neoforged.gradle" version "[6.0.13,7)"
	id "wtf.gofancy.fancygradle" version "[1.1,)"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "com.github.johnrengelman.shadow"  version "[8,9)"
}

repositories {
	jcenter()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		url = "https://repo.spongepowered.org/maven/"
	}
	maven {
		url = "https://maven.su5ed.dev/releases/"
		content {
			includeGroup "net.minecraftforge"
		}
	}
	maven {
		url = "https://maven.blamejared.com/"
		content {
			includeGroup "mezz.jei"
			includeGroup "CraftTweaker2"
		}
	}
	maven {
		url = "https://dvs1.progwml6.com/files/maven/"
		content {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights"
		}
	}
	maven {
		url = "https://maven.amadornes.com/"
		content {
			includeGroup "MCMultiPart2"
		}
	}
	//maven {
	//	url = "https://maven.ic2.player.to/"
	//	content {
	//		includeGroup "net.industrial-craft"
	//		includeGroup "net.sengir.forestry"
	//	}
	//}
	maven {
		url = "https://maven.tterrag.com/"
		content {
			includeGroup "com.enderio.core"
			includeGroup "com.enderio"
			includeGroup "info.loenwind.autoconfig"
			includeGroup "info.loenwind.autosave"
		}
	}
	maven {
		url = "https://maven.modmuss50.me/"
		content {
			includeGroup "RebornCore"
			includeGroup "TechReborn"
		}
	}
	maven {
		url = "https://maven.covers1624.net/"
		content {
			includeGroup "codechicken"
		}
	}
	maven {
		url = "https://maven.shadowfacts.net/"
		content {
			includeGroup "net.shadowfacts"
		}
	}
	//maven {
	//	url = "https://maven.mcmoddev.com/"
	//	content {
	//		includeGroup "net.ndrei"
	//	}
	//}
	maven {
		url = "https://www.mod-buildcraft.com/maven/"
		content {
			includeGroup "com.mod-buildcraft"
		}
	}
	maven {
		url = "https://maven.cleanroommc.com/"
		content {
			includeGroup "zone.rong"
		}
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

def mcVersion = "1.12.2"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "2.3"
def vendor = "thelm"

group = "thelm.jaopca"
version = "2.3.8.13"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "stable", version: "39-1.12"
	generateRunFolders = true
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			environment "MC_VERSION", mcVersion
			jvmArgs "-Dmixin.hotSwap=true"
			mods {
				"${modId}" {
					source sourceSets.main
				}
			}
		}
		client {
			arg "--username=Dev"
			jvmArg "-Xmx7G"
		}
		server {
			jvmArg "-Xmx3G"
		}
	}
}

fancyGradle {
	patches {
		resources
		coremods
		codeChickenLib
		asm
		mergetool
	}
}

configurations {
	include
	implementation.extendsFrom(include)
}

dependencies {
	minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2859"

	include "com.electronwill.night-config:core:3.6.4"
	include "com.electronwill.night-config:toml:3.6.4"

	implementation fg.deobf("mezz.jei:jei_1.12.2:4.16.1.302")
	implementation fg.deobf("slimeknights.mantle:Mantle:1.12-1.3.3.55")
	implementation fg.deobf("slimeknights:TConstruct:1.12.2-2.13.0.183")
	implementation fg.deobf("MCMultiPart2:MCMultiPart:2.5.3")
	implementation fg.deobf("RebornCore:RebornCore-1.12.2:3.19.4.529:universal") {
		transitive = false
	}
	implementation fg.deobf("TechReborn:TechReborn-1.12.2:2.27.3.1084:universal") {
		transitive = false
	}
	implementation fg.deobf("TechReborn:TechReborn-ModCompatibility-1.12.2:1.4.0.76:universal") {
		transitive = false
	}
	implementation fg.deobf("codechicken:CodeChickenLib:1.12.2-3.2.3.358:universal")
	implementation "net.shadowfacts:Forgelin:1.8.4"
	implementation fg.deobf("CraftTweaker2:ZenScript:4.1.20.697")
	implementation fg.deobf("CraftTweaker2:CraftTweaker2-API:4.1.20.697")
	implementation fg.deobf("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.697")
	implementation fg.deobf("com.enderio.core:EnderCore:1.12.2-0.5.78")
	implementation fg.deobf("com.enderio.core:EnderCore:1.12.2-0.5.78:core")
	implementation fg.deobf("com.enderio:EnderIO:1.12.2-5.3.72") {
		transitive = false
	}
	implementation fg.deobf("info.loenwind.autoconfig:AutoConfig:1.12.2-1.0.2")
	implementation fg.deobf("info.loenwind.autosave:AutoSave:1.12.2-1.0.11")
	implementation fg.deobf("com.mod-buildcraft:buildcraft-main:7.99.24.8")
	//implementation fg.deobf("net.ndrei:tesla-core-lib:1.12.2-1.0.18.28") {
	//	transitive = false
	//}
	//implementation fg.deobf("net.ndrei:powered-thingies:1.12.2-1.0.15.25") {
	//	transitive = false
	//}
	//implementation fg.deobf("net.ndrei:bc-ore-processing:7.99.24.4-1.18") {
	//	transitive = false
	//}
	//implementation fg.deobf("net.industrial-craft:industrialcraft-2:2.8.222-ex112")
	//implementation fg.deobf("net.sengir.forestry:forestry_1.12.2:5.8.2.422")

	implementation fg.deobf("curse.maven:wrapup-275153:2486198")
	implementation fg.deobf("curse.maven:advanced-rocketry-236542:4671856")
	implementation fg.deobf("curse.maven:baubles-227083:2518667")
	implementation fg.deobf("curse.maven:embers-rekindled-300777:3695248")
	implementation fg.deobf("curse.maven:ignition-foundry-283807:2719054")
	implementation fg.deobf("curse.maven:futurepack-237333:2856874")
	implementation fg.deobf("curse.maven:libvulpes-236541:3801015")
	implementation fg.deobf("curse.maven:magneticraft-224808:3791484")
	implementation fg.deobf("curse.maven:mekanism-268560:2835175")
	implementation fg.deobf("curse.maven:modelloader-277663:2744735")
	implementation fg.deobf("curse.maven:placebo-283644:4629597")
	implementation fg.deobf("curse.maven:thaumcraft-223628:2629023")
	implementation fg.deobf("curse.maven:thaumic-jei-285492:2705304")
	implementation fg.deobf("curse.maven:applied-energistics-2-223794:2747063")
	implementation fg.deobf("curse.maven:redstone-flux-270789:2920436")
	implementation fg.deobf("curse.maven:cofh-core-69162:2920433")
	implementation fg.deobf("curse.maven:cofh-world-271384:2920434")
	implementation fg.deobf("curse.maven:thermal-foundation-222880:2926428")
	implementation fg.deobf("curse.maven:thermal-expansion-69163:2926431")
	implementation fg.deobf("curse.maven:thermal-dynamics-227443:2920505")
	implementation fg.deobf("curse.maven:abyssalcraft-53686:4856547")
	implementation fg.deobf("curse.maven:minestrappolation-5-233351:2860018")
	implementation fg.deobf("curse.maven:buildcraft-compat-233140:3204471")
	implementation fg.deobf("curse.maven:hbms-nuclear-tech-mod-reloaded-435883:4892080")
	implementation fg.deobf("curse.maven:railcraft-51195:3853491")
	implementation fg.deobf("curse.maven:cyclops-core-232758:3159497")
	implementation fg.deobf("curse.maven:common-capabilities-247007:3053017")
	implementation fg.deobf("curse.maven:integrated-dynamics-236307:3159505")
	implementation fg.deobf("curse.maven:astral-sorcery-241721:3044416")
	implementation fg.deobf("curse.maven:dummycore-unofficial-266491:2611426")
	implementation fg.deobf("curse.maven:essentialcraft-4-unofficial-254817:2611428")
	implementation fg.deobf("curse.maven:tesla-core-lib-254602:3438487")
	implementation fg.deobf("curse.maven:powered-thingies-264283:2556740")
	implementation fg.deobf("curse.maven:bc-ore-processing-277910:2882663")
	implementation fg.deobf("curse.maven:industrial-craft-242638:3838713")
	implementation fg.deobf("curse.maven:forestry-59751:2918418")
	implementation fg.deobf("curse.maven:nuclearcraft-overhauled-336895:3862197")
	implementation fg.deobf("curse.maven:qmd-362056:4975576")

	//Performance
	implementation fg.deobf("curse.maven:reid-629017:4949840")
	implementation("zone.rong:mixinbooter:7.1") {
		transitive = false
	}
	implementation fg.deobf("curse.maven:vintagefix-871198:4800687")
	implementation fg.deobf("curse.maven:lolasm-460609:4800875")
}

configurations.all {
	resolutionStrategy {
		force "net.minecraftforge:legacydev:0.2.4.1"
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
]

jar {
	enabled = false
	finalizedBy("shadowJar")
}

shadowJar {
	archiveClassifier = ""
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	configurations = [project.configurations.include]
	manifest.attributes(manifestAttributes)
	finalizedBy("reobfJar")
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}
