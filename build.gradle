plugins {
	id "com.crystaelix.loom" version "jitpack-afd363f1"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dvs1.progwml6.com/files/maven/"
			}
		}
		filter {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights.tconstruct"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.theillusivec4.top/"
			}
		}
		filter {
			includeGroup "top.theillusivec4.curios"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.architectury.dev/"
			}
		}
		filter {
			includeGroup "dev.architectury"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.saps.dev/releases/"
			}
		}
		filter {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.tterrag.com/"
			}
		}
		filter {
			includeGroup "com.tterrag.registrate"
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.simibubi.create"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
			}
		}
		filter {
			includeGroup "com.eliotlash.mclib"
			includeGroup "software.bernie.geckolib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "com.teamcofh"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1/"
			}
		}
		filter {
			includeGroup "com.kotori316"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://jenkins.bdew.net/maven/"
			}
		}
		filter {
			includeGroup "net.bdew"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dogforce-games.com/maven/"
			}
		}
		filter {
			includeGroup "dev.gigaherz.jsonthings"
			includeGroup "dev.gigaherz.graph"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
			}
		}
		filter {
			includeGroup "net.tslat.smartbrainlib"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
			}
		}
		filter {
			includeGroup "com.klikli_dev"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.20.1"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.jaopca"
version = "4.4.7.9"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	forge {
		mixinConfig "${modId}.mixins.json"
		useForgeLoggerConfig = true
	}
	mixin.defaultRefmapName = "${modId}.refmap.json"
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "forge.logging.console.level", "debug"
		}
		client {
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.20.1"
	mappings loom.layered() {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip"
	}
	forge "net.minecraftforge:forge:1.20.1-47.2.20"

	// Common
	modImplementation("mezz.jei:jei-1.20.1-forge:15.3.0.1") {transitive = false}
	modImplementation "com.tterrag.registrate:Registrate:MC1.20-1.3.11"
	modImplementation "top.theillusivec4.curios:curios-forge:5.7.0+1.20.1"
	forgeRuntimeLibrary "com.eliotlash.mclib:mclib:20"
	modImplementation("software.bernie.geckolib:geckolib-forge-1.20.1:4.4.2") {transitive = false}
	modImplementation "vazkii.patchouli:Patchouli:1.20.1-84-FORGE"
	modImplementation "dev.architectury:architectury-forge:9.1.13"

	// CraftTweaker
	modImplementation("com.blamejared.crafttweaker:CraftTweaker-forge-1.20.1:14.0.32") {transitive = false}

	// KubeJS
	implementation "dev.latvian.mods:rhino-forge:2001.2.2-build.18"
	modImplementation("dev.latvian.mods:kubejs-forge:2001.6.4-build.120") {transitive = false}

	// Json Things
	modImplementation "dev.gigaherz.jsonthings:JsonThings-1.20.1:0.9.1"

	// Open Loader
	modImplementation "net.darkhax.openloader:OpenLoader-Forge-1.20.1:19.0.3"

	// Blood Magic
	modImplementation "curse.maven:blood-magic-224791:4779711"

	// Create
	modImplementation "com.jozufozu.flywheel:flywheel-forge-1.20.1:0.6.10-8"
	modImplementation("com.simibubi.create:create-1.20.1:0.5.1.f-26:slim") {transitive = false}

	// Crossroads
	modImplementation "curse.maven:essentials-293752:4640300"
	modImplementation "curse.maven:crossroads-mc-250231:5047868"

	// Cyclic
	modImplementation "curse.maven:flib-661261:4724762"
	modImplementation "curse.maven:cyclic-239286:4994392"

	// Electrodynamics
	modImplementation "curse.maven:electrodynamics-316867:5022907"

	// Embers Rekindled
	modImplementation "curse.maven:embers-rekindled-300777:5031242"

	// Ender IO
	implementation "dev.gigaherz.graph:GraphLib3:3.0.5"
	modImplementation "curse.maven:ender-io-64578:4920828"

	// Energized Power
	modImplementation "curse.maven:energized-power-782147:5057080"

	// Factorium
	implementation("com.kotori316:scalablecatsforce:3.3.0-build-2:with-library") {transitive = false}
	modImplementation "curse.maven:bdlib-70496:4670650"
	modImplementation "curse.maven:factorium-583423:4670915"

	// Immersive Engineering
	modImplementation "blusunrize.immersiveengineering:ImmersiveEngineering:1.20.1-10.0.0-169.40"

	// Industrial Reborn

	// Integrated Dynamics
	modImplementation "curse.maven:cyclops-core-232758:5080553"
	modImplementation "curse.maven:common-capabilities-247007:4987207"
	modImplementation "curse.maven:integrated-dynamics-236307:5087871"

	// Mekanism
	modImplementation "mekanism:Mekanism:1.20.1-10.4.5.19"

	// Occultism
	forgeRuntimeLibrary "org.commonmark:commonmark:0.21.0"
	forgeRuntimeLibrary "org.commonmark:commonmark-ext-gfm-strikethrough:0.21.0"
	forgeRuntimeLibrary "org.commonmark:commonmark-ext-ins:0.21.0"
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-forge-1.20.1:1.13"
	modImplementation("com.klikli_dev:modonomicon-1.20.1-forge:1.58.0") {transitive = false}
	modImplementation("com.klikli_dev:occultism-1.20.1:1.114.0") {transitive = false}

	// Railcraft Reborn
	modImplementation "curse.maven:railcraft-reborn-901491:5018052"

	// Thermal Expansion
	modImplementation "com.teamcofh:cofh_core:1.20.1-11.0.0.51"
	modImplementation "com.teamcofh:thermal_core:1.20.1-11.0.1.17"
	modImplementation("com.teamcofh:thermal_foundation:1.20.1-11.0.1.63:all") {transitive = false}
	modImplementation("com.teamcofh:thermal_expansion:1.20.1-11.0.0.27") {transitive = false}
	modImplementation("com.teamcofh:thermal_integration:1.20.1-11.0.0.23") {transitive = false}

	// Voluminous Energy
	modImplementation "curse.maven:voluminous-energy-416704:4627288"

	// Testing
	modImplementation "curse.maven:ato-405593:4844097"
	modImplementation "curse.maven:allthemodium-364466:4892369"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:4680832"
	implementation "io.github.llamalad7:mixinextras-forge:0.3.5"
	modImplementation "curse.maven:modernfix-790626:5077298"
	modImplementation "curse.maven:ferritecore-429235:4810975"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = STABLE
	modLoaders.addAll("forge", "neoforge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		projectId = "266936"
		minecraftVersions.add(mcVersion)
	}

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		projectId = "P48mHi2j"
		minecraftVersions.add(mcVersion)
	}
}
